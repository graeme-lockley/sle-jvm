export
  main

import file:../Data/List
import file:../Data/Option
import file:../Data/String
import file:./Actor
import file:./Actor/Response

import file:../Debug as D


console =
  Actor.createBuiltin "za.co.no9.sle.runtime.actors.ConsoleActor"


mod n m =
  n - ((Option.withDefault 1 (n / m)) * m)


isDivisibleBy1000 n =
  (mod n 1000) == 0


sssActor : () -> Actor.Actor Int
sssActor () =
    Actor.filter isDivisibleBy1000
      <| Actor.map String.fromInt
      <| Actor.map (String.append "Value: ")
      <| console


atLimit state =
  (mod state 100000) == 0

incActor : Actor.Actor Int -> Int -> Actor.Actor ()
incActor next initValue =
  let
    init self =
      ((initValue, self), [ Actor.cmd self () ])

    update (state, self) message =
      if atLimit state then
        Response.none
      else
        Response.stateMsgs
          (state + 1, self)
          [ Actor.cmd next state
          , Actor.cmd self ()
          ]
  in
    Actor.create init update


typealias Inc2ActorState =
  { counter: Int
  , self: Actor.Actor ()
  }


inc2Actor : Actor.Actor Int -> Int -> Actor.Actor ()
inc2Actor next initValue =
  let
    init self =
      ( { counter = initValue, self = self }
      , [ Actor.cmd self () ]
      )

    update : Inc2ActorState -> () -> Actor.Response Inc2ActorState
    update state message =
      if atLimit state.counter then
        Response.none
      else
        Response.stateMsgs
          { state | counter = state.counter + 1 }
          [ Actor.cmd next state.counter
          , Actor.cmd state.self ()
          ]
  in
    Actor.create init update


main : Int
main =
  List.repeat 1 100
    |> List.map (\n -> incActor (sssActor ()) 1)
    |> List.length
